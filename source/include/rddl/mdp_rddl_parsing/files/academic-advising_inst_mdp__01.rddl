/////////////////////////////////////////////////////////////////////////////////
//                                                                             //
//                                                                             //
// RDDL MDP version of the IPC 2018 Academic Advising domain.                  //
//                                                                             //
//                                                                             //
// Created and modified for the probabilistic tracks of IPC 2014 by            //
//                                                                             //
//        Libby Ferland (libby.knouse [at] uky.edu) and                        //
//        Scott Sanner (ssanner [at] mie.utoronto.ca)                          //
//                                                                             //
// and modified for the probabilistic tracks of IPC 2018 by                    //
//                                                                             //
//        Thomas Keller (tho.keller [at] unibas.ch).                           //
//                                                                             //
//                                                                             //
// In this domain, a student may take courses at a given cost and passes the   //
// course with a probability determined by how many of the prerequisites they  //
// have successfully passed. A student also receives a penalty at each time    //
// step if they have not yet graduated from their program (i.e., completed all //
// required courses). We allow multiple courses to be taken in a semester in   //
// some instances. Furthermore, instances differ in the depth of the course    //
// dependency DAG, the number of courses, in the cost of taking or retaking    //
// a course, in the number of program requirements and in the horizon.         //
//                                                                             //
//                                                                             //
// Instances were created such that a policy that ignores all dependencies     //
// between courses and takes each course that is a program requirement until   //
// it succeeds is better than a policy that does nothing at all. The expected  //
// reward of this policy is reported in the instance files.                    //
//                                                                             //
//                                                                             //
/////////////////////////////////////////////////////////////////////////////////

domain academic-advising_mdp {
    requirements { 
        reward-deterministic,
        preconditions
    };


    types {
        course : object;
    };


    pvariables {
        //////////////////// non-fluents ////////////////////
        
        // number of courses that can be taken in parallel, (introduced for IPC
        // 2018, replaces max-nondef-actions of IPC 2014 domain)
        COURSES_PER_SEMESTER              : { non-fluent, int, default = 1 };
        
        // first argument is a prerequisite of second argument
        PREREQ(course, course)            : { non-fluent, bool, default = false };

        // probability of passing a course without prerequisites
        PRIOR_PROB_PASS_NO_PREREQ(course) : { non-fluent, real, default = 0.8 };

        // base probability of passing a course with prerequisites (if no
        // prerequisite has been passed)
        PRIOR_PROB_PASS(course)           : { non-fluent, real, default = 0.2 };
    
        // program requirements for graduation
        PROGRAM_REQUIREMENT(course)       : { non-fluent, bool, default = false };
    
        // cost for taking a course the first time
        COURSE_COST                       : { non-fluent, real, default = -1 };

        // cost for taking a course except the first time
        COURSE_RETAKE_COST                : { non-fluent, real, default = -2 };

        // penalty per step for incomplete program
        PROGRAM_INCOMPLETE_PENALTY        : { non-fluent, real, default = -5 };


        //////////////////// state-fluents ////////////////////

        // course has been taken successfully
        passed(course)                    : { state-fluent, bool, default = false };

        // course has been taken at least once
        taken(course)                     : { state-fluent, bool, default = false };


        //////////////////// action-fluents ////////////////////

        // take a course
        take-course(course)               : { action-fluent, bool, default = false };
    };


    cpfs {
        passed'(?c) =
            // if ?c is taken and has no prerequisites, it's passed according to
            // a prior probability
            if ( take-course(?c) & ~( exists_{?c2 : course} [ PREREQ(?c2,?c) ] ) )
                then Bernoulli( PRIOR_PROB_PASS_NO_PREREQ(?c) )

            // if ?c is taken and has no prerequisites, it's passed according to
            // a prior probability and a bonus depending on passed prerequisites
            else if ( take-course(?c) )
                then Bernoulli( PRIOR_PROB_PASS(?c) +
                                ( (1 - PRIOR_PROB_PASS(?c) ) * ( sum_{?c2 : course} [ PREREQ(?c2,?c) & passed(?c2) ] ) /
								  (1 + sum_{?c2 : course} [ PREREQ(?c2,?c) ] ) ) )

            // otherwise, the value persists
            else passed(?c);


        // ?c is taken if it has been taken earlier or is taken now
        taken'(?c) = taken(?c) | take-course(?c);
    };


    reward =
        // taking a course for the first time incurs a cost
        ( sum_{ ?c : course } [ COURSE_COST * ( take-course(?c) & ~taken(?c) ) ] ) +
        
        // taking a course that has been taken before incurs a (usually higher) cost
        ( sum_{ ?c : course } [ COURSE_RETAKE_COST * ( take-course(?c) & taken(?c) ) ] ) +

        // as long as the program is not completed, a penalty is incurred
        ( PROGRAM_INCOMPLETE_PENALTY * ~( forall_{ ?c : course } [ PROGRAM_REQUIREMENT(?c) => passed(?c) ] ) );


    action-preconditions {
        // only take courses that haven't been passed
        ( forall_{ ?c : course } [ take-course(?c) => ~passed(?c) ] );

        // replaces max-nondef-actions
        ((sum_{?c : course} [take-course(?c)]) <= COURSES_PER_SEMESTER);
    };
}     

instance academic-advising_inst_mdp__01 {
    domain = academic-advising_mdp;
    objects {
       course : { c0000, c0001, c0002, c0003, c0004, c0100, c0101, c0102, c0103, c0200, c0201, c0202, c0300, c0301, c0302 };
    };

    non-fluents {
        COURSES_PER_SEMESTER = 1;

        // PRIOR PROBS
        PRIOR_PROB_PASS_NO_PREREQ(c0000) = 0.80;
        PRIOR_PROB_PASS_NO_PREREQ(c0001) = 0.55;
        PRIOR_PROB_PASS_NO_PREREQ(c0002) = 0.67;
        PRIOR_PROB_PASS_NO_PREREQ(c0003) = 0.78;
        PRIOR_PROB_PASS_NO_PREREQ(c0004) = 0.75;
        PRIOR_PROB_PASS(c0100) = 0.22;
        PRIOR_PROB_PASS(c0101) = 0.45;
        PRIOR_PROB_PASS(c0102) = 0.41;
        PRIOR_PROB_PASS(c0103) = 0.44;
        PRIOR_PROB_PASS(c0200) = 0.14;
        PRIOR_PROB_PASS(c0201) = 0.07;
        PRIOR_PROB_PASS(c0202) = 0.24;
        PRIOR_PROB_PASS(c0300) = 0.23;
        PRIOR_PROB_PASS(c0301) = 0.08;
        PRIOR_PROB_PASS(c0302) = 0.16;

        // PREREQ
        PREREQ(c0003, c0100);
        PREREQ(c0000, c0100);
        PREREQ(c0004, c0100);
        PREREQ(c0001, c0101);
        PREREQ(c0002, c0101);
        PREREQ(c0000, c0102);
        PREREQ(c0004, c0102);
        PREREQ(c0001, c0103);
        PREREQ(c0001, c0200);
        PREREQ(c0101, c0200);
        PREREQ(c0103, c0201);
        PREREQ(c0002, c0202);
        PREREQ(c0200, c0300);
        PREREQ(c0201, c0301);
        PREREQ(c0201, c0301);
        PREREQ(c0200, c0302);

        // PROGRAM REQUIREMENT
        PROGRAM_REQUIREMENT(c0300);
        PROGRAM_REQUIREMENT(c0202);
        PROGRAM_REQUIREMENT(c0101);
        PROGRAM_REQUIREMENT(c0002);
        PROGRAM_REQUIREMENT(c0001);

        // COURSE COSTS
        COURSE_COST = 0;
        COURSE_RETAKE_COST = 0;

    };

    init-state {
        ~passed(c0000);
    };

    horizon  = 20;

    discount = 1.0;
}